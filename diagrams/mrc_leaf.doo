%---------------------------%
%                           %
%   Title:   mrc_leaf.doo   %
%   Author:  Jérôme Gout    %
%   Date:    19 June 2001   %
%                           %
%---------------------------%
 
\diagram_header { 
  \designer("Valerie Vann");
  \title("Magic Rose Cube - Leaves");
  \diagramer("Jerome Gout");
  \diagram_date(2001);
  \comment("This diagram has been made for a personnal usage only. Do not distribute this diagram as Valerie's request.");
  \comment("Use 6 square, 3 for leaves (green) and 3 for rose petals (rose, yellow or red).");
  \comment("Difficulty : Intermediate - for a 10cm initial squares you'll get a ~4cm cube - Color behind");
  \bottom_margin(15);
  \color_front(white);
  \color_back(0, 81, 0);
}

\visible_area_height(60);

\step {
  \caption("Begin with leaves.");
  \caption("Just pinch half");

  \square(a,b,c,d);

  [ab2, cd2] = \line_to_line([b,c], [a,d], [a,b], [c,d]);
  o = \middle(ab2,cd2);
  [bc2, ad2] = \line_to_line([a,b], [c,d], [b,c], [a,d]);
  \valley_fold(ab2, o, 0, 75);
  \valley_fold(cd2, o, 0, 75);
  \simple_arrow(bc2, ad2, unfold, valley, right);

  % original front color
  \fill(front, a, b, c, d);
}

\step {
  % to avoid the implicit extremities computation
  % (only 5% by default are left)
  \space_fold([ab2, o], 5, 75);
  \space_fold([cd2, o], 5, 75);
  \caption("Fold unfold vertical quarters.");
  [ab1, cd1] = \line_to_line([a,d], [ab2, o], [a,b], [c,d]);
  [ab3, cd3] = \line_to_line([b,c], [ab2, o], [a,b], [c,d]);

  \valley_fold(ab1, cd1);
  \simple_arrow(ad2, [ab1, cd1], unfold, valley, left);
  \valley_fold(ab3, cd3);
  \simple_arrow(bc2, [ab3, cd3], unfold, valley, right);
}

\step {
 \caption("Fold and unfold right top bissector");
 bc1 = \line_to_line(ab1, cd1, b,  [b,c]);
 \valley_fold(bc1, ab1);
 \simple_arrow(b, [bc1, ab1], unfold, valley, right);
}

\step {
 \caption("Fold left bottom bissector");

 ad1 = \line_to_line(cd3,d, ab3, [a,d]);
 \valley_fold(ad1, cd3);
 \simple_arrow(d, [ad1, cd3], none, valley, left);
 i1 = \inter_cut([ab1, cd1], [ad1, cd3]);
 \cut([c,d], cd3);
 \cut([d,a], ad1);
}

\step {
 % do not forget to cut the bottom pinch 
 % to make it move in the next step
 i2 = \inter_cut([o, cd2], [ad1, cd3]);
 \caption("Fold corner using horizontal crease.");
 % moving points
 \move(d, [ad1, cd3]);
 \move(cd2, [ad1, cd3]);
 \move(cd1, [ad1, cd3]);
 % hiding the bissector made in step 3 ([ab1, bc1])
 % we cut it in half
 i3 = \middle(ab1, bc1);
 \cut([ab1, bc1], i3);
 % each half is redrawn masking the i3 extremity to the border
 % of the upper layer
 \fold(i3, ab1, [d, ad1], 5);
 \fold(i3, bc1, [d, cd3], 5);
 % hide the behind part of the bottom pinch
 \hide([o, i2]);
 % redraw the pinch correctly
 \fold(i2, cd2, 40, 20);
 % redraw left existant fold made in step 2
 % it is partially hidden by the top layer
 \fold(i1, ab1, [ad1, d], 10);
 % make the fold line bacoming a border
 \border(ad1, cd3);
 % fill the upper layer
 \fill(back, ad1, d, cd3);
 % fill the front side also
 \unfill(a,b,c,d);
 \fill(front, a, ad1, d, cd3, c, b);
 
 % new fold of this step
 i4 = \line_to_line(cd1, d, o, [ad1, d]);
 \valley_fold(i4, cd1);
 \simple_arrow(d, o, none, valley, right);
 % preparing the move of the next step
 \cut([ad1, d], i4);
 \cut([d, cd3], cd1);
 % since [ad1, d] was a limit for [i1, ab1] 
 % this edge should be redrawn
 \fold(i1, ab1, [ad1, i4], 7);
}

\step {
 \unfill(ad1, d, cd3); % back
 \unfill(a, ad1, d, cd3, c, b); % front
 \move(d, o);
 % fold line becomes broder
 \border(i4, cd1);
 % fill layers
 \fill(back, ad1, i4, d, cd1, cd3);
 \fill(front, a, ad1, i4, d, cd1, cd3, c, b);
 
 % new folds (swivel fold)
 \caption("Swivel fold.");
 \valley_fold(i1, cd1, -20, -20);
 \mountain_fold(i4, i1);
 % concerning [ab1, i1]: we want show in xray the hidden part
 % of the fold. So we have to cut this edge.
 i5 = \inter_cut([ab1, i1], [ad1, i4]);
 \valley_fold(ab1, i5);
 \xray_fold(i5, i1);
 % make the arrow to show the move
 \simple_arrow(ad1, i4, none, valley, left);
 \simple_arrow(i4, i2, none, valley, right);

 % preparing the move of the next step
 \cut([a, b], ab1);  % a moving
 \cut([ad1, i4], i5); % ad1 moving
 \cut([ad1, cd3], i1); 
}

\step {
 % unfill to make it clearer
 \unfill(ad1, i4, d, cd1, cd3); % back
 \unfill(a, ad1, i4, d, cd1, cd3, c, b); % front
 % apply previous folds
 \move(a, ab2);
 \move(ad1, i4);
 \move(i4, i2); % becarful to the order i4 is first used and then moved
 \move(i5, d);
 % vertical fold line becomes border
 % this fold has been cut in previous step to make xray and valley
 \border(ab1, i1);
 % hide masked creases
 \fold(ab1, i3, [ab2, o], 0);
 \hide([ab1, i5]);
 \hide([i1, cd1]);
 % restore bissector made in step 3
 \fold(i3, bc1, 0, 5);
 % border becomes fold
 \fold(i1, ad1);
 % fill faces
 \fill(back, ab1, a, i2, i1);
 \fill(back, cd1, cd3, i2);
 \fill(front, a, i2, cd1, cd3, c, b);

 % new fold
 \caption("Valley fold the right bottom corner.");
 \valley_fold(cd3, bc1, -40, -40);
 \simple_arrow(c, cd2, none, valley, right);

 % preparing the move of the next step
 \cut([b,c], bc1);
}

\step {
 % unfill last front area
 \unfill(a, i2, cd1, cd3, c, b);
 % apply previous fold
 \move(c, cd2);
 % fold line becomes border
 \border(cd3, bc1);
 % fill new back layer
 \fill(back, cd3, c, bc1);
 \fill(front, a, i2, cd1, c, bc1, b);

 % new fold
 \caption("Valley fold up the bottom point.");
 \caption("Fold all the layers.");
 \valley_fold(i2, bc1, -30, -30); % becareful new edge !
 \simple_arrow(cd3, cd1, none, valley, right);

 % preparing the move of the next step
 \cut([cd1, cd3], cd2);
 \cut([i1, cd3], i2);
}

% Zoom in
\scale(130);

\step {
 % unfill area reduce to line
 \unfill(cd1, cd3, i2); % back
 \unfill(a, i2, cd1, c, bc1, b); % front
 % apply previous fold
 \move(cd3, cd1);
 % hide lines which are equal to last fold line
 \hide([i2, cd2]);
 \hide([c, bc1]);
 % hide lines which are behind top layer
 \hide([cd1, cd2]);
 \hide([cd2, cd3]);
 \hide([ab2, o]);
 % fold line becomes border
 \border(i2, bc1);
 % border becomes fold
 \fold(c, cd3);
 % fill correctly layers
 \fill(back, i2, cd3, cd2);
 \fill(front, a, i2, cd1, bc1, b);

 % new folds (swivel II)
 \caption("All folds exist expect the right one.");
 \caption("White upper right part should go behind left green layer.");
 \caption("You can open slightly the green upper left layer.");
 \mountain_fold(ab3, cd3);
 i7 = \symmetry(ad1, [ab3, cd1]);
 \valley_fold(cd1, i7);
 \valley_fold(i3, bc1, 0, -30);
 % because we want show a wray fold under the left layer
 % we have to cut the edge [ab1, i3] made in step 5
 \cut([ab1, i3], ad1);
 \xray_fold(ab1, ad1);
 \valley_fold(ad1, i3);
 \simple_arrow(ab3, i1, none, valley, right);
 \simple_arrow(bc1, i2, none, valley, left);

 % preparing the move of the next step
 \cut([i3, bc1], cd1);
 \cut([ab1, b], ab3);
 \cut([b, bc1], i7);
 \cut([i2, bc1], c);
}

\step {
 % unfill front color
 \unfill(a, i2, cd1, bc1, b);
 % apply previous fold
 \move(b, a);
 \move(b, [ab1, ad1]);
 \move(ab3, i1);
 \move(bc1, i2);
 \move(i7, ad1);
 % hide lines
 \hide([b, i7]);
 \hide([ab3, cd3]);
 \hide([ab1, ab3]);
 \hide([d, cd1]);
 \hide(b);
 % border becomes fold
 \fold(i2, cd1);
 % fold lines become border
 \border(ad1, cd1);
 \border(cd1, c);
 % fill last white triangle
 \fill(back, ad1, cd1, i4);

 % new folds
 \caption("Mountain fold the right top corner.");
 \caption("Let this corner folded 90 degres backwards.");
 \mountain_fold(ab1, ad1);
 \return_arrow([ab1, ad1], [ab1, a], unfold, mountain, right);
}

\rotate(45);
\scale(150);

\step {
 % compute a new center point
 center = \middle(i1, ad1);
 \visible_area_center(center);
 % unfill all previously drawn faces
 \unfill;
 % little 90 degres coner
 \fill(\darker(back), a, ad1, ab1);
 % rest of model
 \fill(back, ab1, ab3, bc1, c, cd1);
 % Fake 3D
 \shift(a, 0, -5);

 % new folds
 \caption("Fold back both lockers to 90 degres.");
 \mountain_fold(i1, ad1, -20, -20);
 \mountain_fold(i2, cd1, -20, -20);
 \return_arrow([i1, ad1], [i1, ab1], none, mountain, right);
 \return_arrow([i2, cd1], [i4, c], none, mountain, left);
}

\step {
 \unfill;
 \caption("Finished Magic Rose Cube leaf.");
 \caption("The inner square will be a cube face.");
 \caption("Its diagonal will guide the assembly");
 \caption("Make three modules like this.");
 
 % fold lines become border
 \border(i1, ad1);
 \border(i4, cd1);
 % make the model 3D again
 \shift(ab1, 14, 4);
 \shift(c, -5, 2);
 % we have to modify the position of a according to ab1 one	 
 \shift(a, 10, -6);
 % fill the square
 \fill(back, ad1, cd1, i2, i1);
 % fill the lockers darker than the square due to 3D !
 \fill(\darker(back), ab1, ad1, i1);
 \fill(\darker(\darker(back)), ab1, a, ad1);
 \fill(\lighter(back), cd1, c, i2);
}
